WHAT: work
WHERE: work
WHEN: 8 - 19
WHO: Actor = getPerson(true); If !Actor.isColleague() && !Actor.isBoss() && Actor:rapportwithplayer > 20 && Actor:intelligence > Random(50, 100)
OTHER: Random(0, 1000) < jobexperience

    SceneStart()
    Actor.dress()
    "Question d'attirer des nouveaux talents, mon entreprise demande à ses employés actuels de recommander des personnes qu'ils connaissent pour être interviewées pour un poste. S'ils s'avèrent être le candidat retenu, la personne qui les a présentés recevrait une prime."
    Actor.show(2)
    "Cela m'a fait penser tout de suite à <Actor.name>, qui, je le sais, est intelligent et n'aime pas son emploi actuel."
    "Dois-je recommander <Actor.name> à mon entreprise?"
    0:: "Oui"
    1:: "Non"
    
    If 0
        "J'ai décidé d'aller de l'avant et j'ai écrit une lettre pour recommander <Actor.name> à l'entreprise, ce qui a valu à <Actor.name> une interview."
        karma += 2.5
        "Quelques semaines plus tard ..."
        If Random(0, 100) < Actor:intelligence
            "Bonne nouvelle! Mon ami <Actor.name> a réussi l'interview et rejoindra bientôt mon entreprise. Nous sommes maintenant aussi collègues. De plus: j'ai reçu un petit bonus pour avoir recommandé un nouveau membre du personnel."
            Actor:rapportwithplayer += Random(0, 10)
            Actor:attractiontoplayer += Random(0, 3)
            money += Random(500, 4000)
            Actor.addColleague()
        Else
            "Malheureusement, <Actor.name> n'a pas pu être à la hauteur des attentes de l'entreprise lors de l'interview et n'a pas eu d'autre choix que de rester à son emploi actuel. Cela m'a un peu mal blessé."
            jobperformance -= Random(0, 0.5)
        Endif
    Else
        "Après réflexion, je ne suis pas tout à fait sûr que <Actor.name> convienne bien à cette entreprise."
    Endif

    SceneEnd()
    timeout(2000, refer_a_friend)