WHAT: all
WHERE: all, -home, -fraternity
WHEN: 7-19
WHO: none
OTHER: Random(0, 300) < interpersonal || [ [WHAT == socialize || WHERE == nightclub] && Random(0, 100) < interpersonal] || [isWithCompanion() && Random(0, 200) < CurrentCompanion:interpersonal] || [WHERE == home && Random(0, 2000) < interpersonal]

    SceneStart()
    Actor = generatePersonTemporary()
    Actor2 = generatePersonTemporary()
    Actor.dress()
    Actor2.dress()
    Actor.show()
    Actor2.show()
    "Juste par hasard, je suis tombé sur un couple, <Actor.name> et <Actor2.name>. On dirait que ce sont des gens décents!"
    0:: "Échanger nos numéros"
    1:: "Peut-être pas"
    
    If 0
        "Nous avons échangé nos numéros. Ce serait bien de mieux les inviter un jour ou vice et versa."
        Actor.makeInterested(Actor2)
        Actor2.makeInterested(Actor)
        If Actor.isInterestedIn(Player) && Actor:attractiontoplayer < 0
            Actor:attractiontoplayer => Random(0, 50)
        Endif
        If Actor2.isInterestedIn(Player) && Actor2:attractiontoplayer < 0
            Actor2:attractiontoplayer => Random(0, 50)
        Endif
        Actor.makePermanent()
        Actor2.makePermanent()
        exchangeContact(Actor)
        exchangeContact(Actor2)
        If Random(0, 100) < Actor:age + Actor2:age
            addNpcRelationship(Spouses, Actor, Actor2)
            "Ils semblent être dans un mariage heureux."
        Else
            addNpcRelationship(Dating, Actor, Actor2)
            "Ils semblent être dans une relation heureuse."
        Endif
        If isModEnabled(vin_Polyamorous) && Random(100, 200) < Actor:perversion + Actor2:perversion
            addNpcRelationship(Poly, Actor, Actor2)
            "Je ne peux pas m'empêcher de remarquer qu'il y a quelque chose d'inhabituel et de très open sur la façon dont ils agissent en couple. Mon instinct me dit qu'ils sont probablement du style polyamoureux."            
        Endif
    Endif
    SceneEnd()
    timeout(150, meet_couple)