WHAT: none
WHERE: none
WHEN: 0 - 24
WHO: none
OTHER: none

    SceneStart()
    If Actor.isDating() || Actor.isRelative() || [Random(30, 100) < interpersonal && [Actor:rapportwithplayer > 30 || Actor:attractiontoplayer > 30]] 
        Actor(Happy):: "Bien sûr, allons chez vous pour relaxer."
        moveTo(home)
        setBackgroundCustom(livingroom)
        "Nous sommes maintenant à l'intérieur de l'appartement."
        choice = -1    
        ate = false
        hadSex = false    
        Vanilla = IsModEnabled(vin_VanillaRealistic)
        MyHome = true
            
        While choice < 5
            Kissed = false
            "Que dois-je suggérer à <Actor.name> de faire?"
            0:: "Discuter"
            1:: !ate:: "Mangez quelque chose ensemble"
            2:: "Regarder un film"
            3:: "Boire de l'alcool"
            4:: !hadSex:: "Embrasser <Actor.name>"
            5:: MyHome:: "Demandez poliment à <Actor.name> de partir"
            
            If 5
                "Nous nous sommes dit au revoir et sommes parti chacun de notre côté."
            Else
                If 0
                    "Nous nous sommes simplement assis et avons bavardé pendant un moment ..."
                    passTime(0.1, 0.2)
                Elseif 1
                    "Nous avons préparé des collations rapides et les avons dégustées ensemble, tout en discutant ..."
                    ate = true
                    passTime(0.2, 0.4)
                Elseif 2
                    "Nous avons regardé un film ensemble ..."
                    passTime(1, 2)
                Elseif 3
                    "Nous avons pris un verre ensemble ..."
                    passTime(0.1, 0.2)
                    Player:intoxication += Random(0,10)
                    Actor:intoxication += Random(0,10)
                Elseif 4
                    Kissed = true
                Endif

                If Kissed || [Actor.isInterestedIn(Player) && !hadSex && Actor:attractiontoplayer > Random(0, 200)]
                    AnimatePair(Player, Actor, Kissing)
                    Player(Kiss):: "..."
                    Actor(Kiss):: "..."
                    If Dating
                        "Nous nous sommes embrassés. Cela a commencé lentement mais rapidement c'est devenu plus passionné. C'était évident ce que nous voulions tous les deux ..."
                    Else
                        "Nous avons eu un baiser, un baiser maladroit au début, mais nous nous sommes rapidement dirigés vers une direction plus passionnée."
                    Endif
                    "Dois-je essayer de baiser avec <Actor.name>?"
                    0:: Vanilla:: "Oui"
                    1:: "Non pas maintenant"
                    
                    If 0
                        If Actor.isDating() || [Actor.isInterestedIn(Player) && [Actor.IsDominantSex(Player) || Actor:attractiontoplayer + Actor:perversion > Random(50, 200) || Actor:intoxication > 80]]
                            "Notre baiser devenait de plus en plus torride que nous avons tous les deux commencer à caresser l'autre un peu partout."
                            If TfGame() && Actor.isMale()
                                "Je suis un homme piégé dans le corps d'une femme. Je ne pense pas que je suis gay, mais me voici, sur le point de baiser avec un gars."
                            Endif
                            hadSex = true
                            Player:perversion += Random(0, 1)
                            Sex(Player, Actor)
                            Actor.show(2)
                            "Le sexe était incroyable. Nous avons remis nos vêtements après et avons continué à discuter."
                            passTime(0.2, 0.5)
                        Else
                            "Malheureusement pour moi, <Actor.name> ne semblait pas intéressé à baiser ou même juste s'en tenir aux préliminaires."
                        Endif
                    Else
                        "J'ai décidé de m'arrêter là et de ne pas aller plus loin."
                    Endif
                Elseif Random(0, 100) < 20
                    If Dating
                        "Au bout d'un moment, nous étions tous les deux épuisés et nous sommes allés nous coucher ensemble pour la nuit."
                        passTime(4, 8)
                        energy += Random(50, 100)
                        "Le lendemain matin ..."
                        If MyHome
                            Actor(Happy):: "Je dois y aller maintenant, chérie. Merci de m'avoir laissé passer la nuit. J'ai hâte de te revoir bientôt!"
                        Else
                            Player(Happy):: "Je dois y aller maintenant, chérie. Merci de m'avoir laissé passer la nuit. J'ai hâte de te revoir bientôt!"
                        Endif
                        Actor:attractiontoplayer += Random(0, 1)
                    Else
                        If MyHome
                            Actor(Happy):: "Je dois y aller maintenant. Merci de m'avoir invité."
                            Player(Happy):: "Très bien. Merci d'être resté. À la prochaine!"
                        Else
                            Actor(Happy):: "Ce fut une belle journée, mais je suis assez fatigué maintenant. Je dois y aller maintenant ..."
                            Player(Happy):: "Allez, repose toi bien, au revoir."
                        Endif
                        "Nous nous sommes séparés."
                    Endif
                    choice = 5            
                Endif        
            Endif
        Endwhile
    Else
        Actor(Sad):: "Désolé, je n'ai pas l'impression de te connaître suffisamment pour t'inviter chez moi."
    Endif    
    SceneEnd()